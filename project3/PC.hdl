// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
        // if inc(t): out(t+1) = out(t) + 1
        // else: out(t+1) = out(t)
        Inc16(in=prevVal, out=incVal);
        Mux16(a=prevVal, b=incVal, sel=inc, out=incOut);

        // if load(t): out(t+1) = in(t)
        // else: out(t+1) = incOut
        Mux16(a=incOut, b=in, sel=load, out=loadOut);

        // if reset(t): out(t+1) = 0
        // else: out(t+1) = loadOut
        Mux16(a=loadOut, b=false, sel=reset, out=resetOut);

        Register(in=resetOut, load=true, out=prevVal, out=out);
}
